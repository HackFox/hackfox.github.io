## BeforeDBGetProp, AfterDBGetProp, BeforeDBSetProp, AfterDBSetProp

These `Database Events` fire when the `DBGetProp()` and `DBSetProp()` functions are used.

### Usage

```foxpro
PROCEDURE DBC_BeforeDBGetProp( cName, cType, cProperty )

PROCEDURE DBC_AfterDBGetProp( cName, cType, cProperty )

PROCEDURE DBC_BeforeDBSetProp( cName, cType, cProperty,
                               ePropertyValue )

PROCEDURE DBC_AfterDBSetProp( cName, cType, cProperty,
                              ePropertyValue )
```
<table>
<tr>
  <td width="32%" valign="top">
  <p><b>Parameter</b></p>
  </td>
  <td width="23%" valign="top">
  <p><b>Value</b></p>
  </td>
  <td width="45%" valign="top">
  <p><b>Meaning</b></p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>cName</p>
  </td>
  <td width="23%" valign="top">
  <p>Character</p>
  </td>
  <td width="45%" valign="top">
  <p>The name of the object in the database.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>cType</p>
  </td>
  <td width="23%" valign="top">
  <p>Character</p>
  </td>
  <td width="45%" valign="top">
  <p>The object type: DATABASE, TABLE, VIEW, FIELD, or CONNECTION.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>cProperty</p>
  </td>
  <td width="23%" valign="top">
  <p>Character</p>
  </td>
  <td width="45%" valign="top">
  <p>The property being read or changed.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>ePropertyValue</p>
  </td>
  <td width="23%" valign="top">
  <p>Character, Numeric, or Logical</p>
  </td>
  <td width="45%" valign="top">
  <p>The new value for the property.</p>
  </td>
 </tr>
</table>

You can prevent the property from being read or changed by returning .F. in the Before events (in which case, the `DBGetProp()` and `DBSetProp()` functions return .NULL.). The After events simply receive notification that `DBGetProp()` and `DBSetProp()` were used.

One use of BeforeDBSetProp is to prevent someone from turning off `Database Events`, therefore bypassing all the security and other things you've implemented `Database Events` for. The first example below demonstrates this.

Another use might be to return different values than are actually stored in the DBC. Say, for example, you want to store captions for fields in different languages, and then at runtime, display the caption for a field in the user's selected language. You might think you could use code like the following in the BeforeDBGetProp event:

```foxpro
IF UPPER(cType) = "FIELD" AND UPPER(cProperty) = "CAPTION"
* Look up the caption for the specified field in a captions table.
* oUser is a global user object and cLanguage is their selected language.
    lcCaption = GetFieldCaption(cName, oUser.cLanguage)
    RETURN lcCaption
ELSE
* Handle any other cases.
ENDIF
```
Unfortunately, this won't work, because `Database Events` can't return values back to the function that resulted in them being fired; they can only return .T. or .F. (in the case of Before events) to indicate that whatever fired them can continue.

Instead, in BeforeDBGetProp, look up the desired value and use `DBSetProp()` to change the property to that value, so that's what the function will return to the caller. In AfterDBGetProp, change the property back to its former value. Thanks to Ted for pointing out this cool solution.

<table>
<tr>
  <td width="17%" valign="top">
<img width="95" height="78" src="bug.gif">
  </td>
  <td width="83%">
  <p>A field name is passed without an alias to the cName parameter. </p>
  </td>
 </tr>
</table>

### Example

```foxpro
* Prevent someone from turning off database events.

PROCEDURE DBC_BeforeDBSetProp(cName, cType, cProperty, ;
    ePropertyValue)
RETURN NOT (UPPER(cName) == UPPER(JUSTSTEM(dbc())) AND ;
    UPPER(cType) = "DATABASE" AND ;
    INLIST(UPPER(cProperty), "DBCEVENTS", "DBCEVENTFILE"))

* Return your own custom string rather than the underlying
* property.

PROCEDURE DBC_BeforeDBGetProp(cName, cType, cProperty)
IF UPPER(cName) = "COMPANY" AND ;
   UPPER(cType) = "FIELD" AND ;
   UPPER(cProperty) = "CAPTION"
    DBSETPROP("Customer.Company", "Field", "Caption", "Nom")
ENDIF

PROCEDURE DBC_AfterDBGetProp(cName, cType, cProperty)
IF UPPER(cName) = "COMPANY" AND ;
   UPPER(cType) = "FIELD" AND ;
   UPPER(cProperty) = "CAPTION"
    DBSETPROP("Customer.Company", "Field", "Caption", "Name")
ENDIF
```
### See Also

[Database Events](s4g900.md), [DBGetProp()](s4g350.md), [DBSetProp()](s4g350.md)