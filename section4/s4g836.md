## BeforeAppendProc, AfterAppendProc, BeforeCopyProc, AfterCopyProc, BeforeModifyProc, AfterModifyProc

These `Database Events` fire when the stored procedures of a database are manipulated in some way: appended from a file, copied to a file, or modified in a code window.

### Usage

```foxpro
PROCEDURE DBC_BeforeAppendProc( cFileName, nCodePage,
                                lOverwrite )

PROCEDURE DBC_AfterAppendProc( cFileName, nCodePage,
                               lOverwrite )

PROCEDURE DBC_BeforeCopyProc( cFileName, nCodePage, lAdditive )

PROCEDURE DBC_AfterCopyProc( cFileName, nCodePage, lAdditive )

PROCEDURE DBC_BeforeModifyProc()

PROCEDURE DBC_AfterModifyProc( lChanged )
```
<table>
<tr>
  <td width="32%" valign="top">
  <p><b>Parameter</b></p>
  </td>
  <td width="23%" valign="top">
  <p><b>Value</b></p>
  </td>
  <td width="45%" valign="top">
  <p><b>Meaning</b></p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>cFileName</p>
  </td>
  <td width="23%" valign="top">
  <p>Character</p>
  </td>
  <td width="45%" valign="top">
  <p>The name of the file that procedures are appended from or copied to.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>nCodePage</p>
  </td>
  <td width="23%" valign="top">
  <p>Numeric</p>
  </td>
  <td width="45%" valign="top">
  <p>The codepage for the file. VFP will automatically convert from that codepage to the codepage for stored procedures when appending procedures or vice versa when copying procedures.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>lOverwrite</p>
  </td>
  <td width="23%" valign="top">
  <p>Logical</p>
  </td>
  <td width="45%" valign="top">
  <p>Indicates whether the OVERWRITE clause was included in the APPEND PROCEDURES command.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>lAdditive</p>
  </td>
  <td width="23%" valign="top">
  <p>Logical</p>
  </td>
  <td width="45%" valign="top">
  <p>Indicates whether the ADDITIVE clause was included in the COPY PROCEDURES command.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>lChanged</p>
  </td>
  <td width="23%" valign="top">
  <p>Logical</p>
  </td>
  <td width="45%" valign="top">
  <p>Indicates whether the stored procedures were changed.</p>
  </td>
 </tr>
</table>

As is the case with other before-and-after pairs of events, you can prevent the action from taking place by returning .F. in the Before events, while the After events simply receive notification that something happened. The Before events of these three sets, along with BeforeDBSetProp, are key to preventing someone from seeing or altering your stored procedure code; see "`Database Events`" for details.

### Example

```foxpro
* Prevent everyone except developers from seeing or altering the
* stored procedures. Store the current stored procedures in a
* "before" snapshot in case we want to restore them later.

PROCEDURE DBC_BeforeAppendProc(cFileName, nCodePage, lOverwrite)
LOCAL llOK
llOK = IsDeveloper()
IF llOK
    DO SaveProcs
ENDIF llOK
RETURN llOK

PROCEDURE DBC_BeforeCopyProc(cFileName, nCodePage, lAdditive)
RETURN IsDeveloper()

PROCEDURE DBC_BeforeModifyProc()
LOCAL llOK
llOK = IsDeveloper()
IF llOK
    DO SaveProcs
ENDIF llOK
RETURN llOK

* Log when someone changes the stored procedures.

PROCEDURE DBC_AfterAppendProc(cFileName, nCodePage, lOverwrite)
DO LogDBCChange WITH IIF(lOverwrite, "Overwritten by ", ;
    "Appended from ") + cFileName

PROCEDURE DBC_AfterModifyProc(lChanged)
IF lChanged
    DO LogDBCChange WITH "Modified"
ENDIF lChanged
```
### See Also

[Append Procedures](s4g343.md), [BeforeDBSetProp](s4g837.md), [ModifyData](s4g861.md), [Copy Procedures](s4g343.md), [Database Events](s4g900.md), [Modify Procedure](s4g605.md)