## BeforeValidateData, AfterValidateData, PackData

The first two `Database Events` fire when a database is validated using `VALIDATE DATABASE`. The last one fires when it's packed, either programmatically with `PACK DATABASE` or visually by choosing "Clean Up Database" from the Database menu.

### Usage

```foxpro
PROCEDURE DBC_BeforeValidateData( lRecover, lNoConsole, lPrint,
                                  lFile [, cFileName ] )

PROCEDURE DBC_AfterValidateData( lRecover, lNoConsole, lPrint,
                                 lFile [, cFileName ] )
```
<table>
<tr>
  <td width="32%" valign="top">
  <p><b>Parameter</b></p>
  </td>
  <td width="23%" valign="top">
  <p><b>Value</b></p>
  </td>
  <td width="45%" valign="top">
  <p><b>Meaning</b></p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>lRecover</p>
  </td>
  <td width="23%" valign="top">
  <p>Logical</p>
  </td>
  <td width="45%" valign="top">
  <p>Indicates whether the RECOVER clause was specified.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>lNoConsole</p>
  </td>
  <td width="23%" valign="top">
  <p>Logical</p>
  </td>
  <td width="45%" valign="top">
  <p>Indicates whether the NOCONSOLE clause was used.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>lPrint</p>
  </td>
  <td width="23%" valign="top">
  <p>Logical</p>
  </td>
  <td width="45%" valign="top">
  <p>Indicates whether the TO PRINTER clause was used.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>lFile</p>
  </td>
  <td width="23%" valign="top">
  <p>Logical</p>
  </td>
  <td width="45%" valign="top">
  <p>Indicates whether the TO FILE clause was specified.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>cFileName</p>
  </td>
  <td width="23%" valign="top">
  <p>Character</p>
  </td>
  <td width="45%" valign="top">
  <p>The name of the file output is directed to.</p>
  </td>
 </tr>
</table>

<table>
<tr>
  <td width="17%" valign="top">
<img width="95" height="94" src="design.gif">
  </td>
  <td width="83%">
  <p>If the TO FILE clause isn't specified, the cFileName parameter isn't passed (PCOUNT() returns 4), but be sure to include it in the parameter list or you'll get a &quot;Must specify additional parameters&quot; error if TO FILE is used.</p>
  </td>
 </tr>
</table>

As is the case with other before-and-after pairs of events, you can prevent a database from being validated by returning .F. in the BeforeValidateData event, while the AfterValidateData event simply receives notification that something happened.

<table>
<tr>
  <td width="17%" valign="top">
<img width="95" height="77" src="bug.gif">
  </td>
  <td width="83%">
  <p>BeforeValidateData and AfterValidateData don't fire when the VALIDATE clause is used with the OPEN DATABASE command. However, the OpenData event receives .T. for the lValidate parameter, so if you need to do something whenever the database is validated, you could call the same routine BeforeValidateData does when OpenData receives .T. for that parameter's value. This is another example that proves the rule &quot;Events call methods.&quot;</p>
  </td>
 </tr>
</table>

### Example

```foxpro
* This code goes in the stored procedures of a database.

PROCEDURE DBC_BeforeValidateData(lRecover, lNoConsole, lPrint, ;
    lFile, cFileName)
WAIT WINDOW PROGRAM() + CHR(13) + ;
    'lRecover: ' + TRANSFORM(lRecover) + CHR(13) + ;
    'lNoConsole: ' + TRANSFORM(lNoConsole) + CHR(13) + ;
    'lPrint: ' + TRANSFORM(lPrint) + CHR(13) + ;
    'lFile: ' + TRANSFORM(lFile) + CHR(13) + ;
    'cFileName: ' + TRANSFORM(cFileName)

PROCEDURE DBC_AfterValidateData(lRecover, lNoConsole, lPrint, ;
    lFile, cFileName)
WAIT WINDOW PROGRAM() + CHR(13) + ;
    'lRecover: ' + TRANSFORM(lRecover) + CHR(13) + ;
    'lNoConsole: ' + TRANSFORM(lNoConsole) + CHR(13) + ;
    'lPrint: ' + TRANSFORM(lPrint) + CHR(13) + ;
    'lFile: ' + TRANSFORM(lFile) + CHR(13) + ;
    'cFileName: ' + TRANSFORM(cFileName)

* End of stored procedures.
* Validate a database.

OPEN DATABASE TestData EXCLUSIVE
VALIDATE DATABASE RECOVER TO PRINT
```
### Usage

```foxpro
PROCEDURE DBC_PackData()
```

There are no before and after events for packing a database; the PackData event fires before packing occurs, so return .F. to prevent packing from taking place (you'll get a "file access denied" error in that case). This might be an issue if you need to do something after a database is packed, although we can't really think of anything useful you'd want to do.

<table>
<tr>
  <td width="17%" valign="top">
<img width="95" height="94" src="design.gif">
  </td>
  <td width="83%">
  <p>As explained in the topic for PACK DATABASE, you can use the PACK command to pack a database if you open it as a table. However, since it isn't open as a database when you do that, the PackData event doesn't fire.</p>
  </td>
 </tr>
</table>

### Example

```foxpro
* This code goes in the stored procedures of a database.

PROCEDURE DBC_PackData()
WAIT WINDOW PROGRAM()

* End of stored procedures.
* Pack a database.

OPEN DATABASE TestData EXCLUSIVE
PACK DATABASE
```
### See Also

[Database Events](s4g900.md), [Open Database](s4g316.md), [OpenData](s4g861.md), [Pack](s4g073.md), [Pack Database](s4g318.md), [Validate Database](s4g319.md)