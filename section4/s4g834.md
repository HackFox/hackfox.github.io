## BeforeAddRelation, AfterAddRelation, BeforeDropRelation, AfterDropRelation

There are only two types of `Database Events` for relations: adding and removing. When a relation is modified, it's first removed and then re-added, so both the remove and add events fire.

### Usage

```foxpro
PROCEDURE DBC_BeforeAddRelation( cRelationID, cTableName,
                                 cRelatedChild, cRelatedTable,
                                 cRelatedTag )

PROCEDURE DBC_AfterAddRelation( cRelationID, cTableName,
                  cRelatedChild, cRelatedTable,
                                cRelatedTag )

PROCEDURE DBC_BeforeDropRelation( cRelationID, cTableName,
                                  cRelatedChild, cRelatedTable,
                                  cRelatedTag )

PROCEDURE DBC_AfterDropRelation( cRelationID, cTableName,
                                 cRelatedChild, cRelatedTable,
                                 cRelatedTag )
```
<table>
<tr>
  <td width="32%" valign="top">
  <p><b>Parameter</b></p>
  </td>
  <td width="23%" valign="top">
  <p><b>Value</b></p>
  </td>
  <td width="45%" valign="top">
  <p><b>Meaning</b></p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>cRelationID</p>
  </td>
  <td width="23%" valign="top">
  <p>Character</p>
  </td>
  <td width="45%" valign="top">
  <p>The name of the relation (which is often something like &quot;Relation 1&quot;).</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>cTableName</p>
  </td>
  <td width="23%" valign="top">
  <p>Character</p>
  </td>
  <td width="45%" valign="top">
  <p>The name of the child table.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>cRelatedChild</p>
  </td>
  <td width="23%" valign="top">
  <p>Character</p>
  </td>
  <td width="45%" valign="top">
  <p>Although you can't tell from the dumb name for this parameter, it's the tag for the child table. </p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>cRelatedTable</p>
  </td>
  <td width="23%" valign="top">
  <p>Character</p>
  </td>
  <td width="45%" valign="top">
  <p>The name of the parent table.</p>
  </td>
 </tr>
<tr>
  <td width="32%" valign="top">
  <p>cRelatedTag</p>
  </td>
  <td width="23%" valign="top">
  <p>Character</p>
  </td>
  <td width="45%" valign="top">
  <p>The tag for the parent table.</p>
  </td>
 </tr>
</table>

As is the case with other before-and-after pairs of events, you can prevent a relation from being added or removed by returning .F. in the BeforeAddRelation and BeforeDropRelation events, while the AfterAddRelation and AfterDropRelation events simply receive notification that something happened.

### Example

```foxpro
* This goes in the stored procedures of the database.

PROCEDURE DBC_AfterDropRelation(cRelationID, cTableName, ;
    cRelatedChild, cRelatedTable, cRelatedTag)
WAIT WINDOW PROGRAM() + CHR(13) + ;
    "Child: " + cTableName + "." + cRelatedChild + CHR(13) + ;
    "Parent: " + cRelatedTable + "." + cRelatedTag

PROCEDURE DBC_BeforeDropRelation(cRelationID, cTableName, ;
    cRelatedChild, cRelatedTable ,cRelatedTag)
WAIT WINDOW PROGRAM() + CHR(13) + ;
    "Child: " + cTableName + "." + cRelatedChild + CHR(13) + ;
    "Parent: " + cRelatedTable + "." + cRelatedTag

PROCEDURE DBC_AfterAddRelation(cRelationID, cTableName, ;
   cRelatedChild, cRelatedTable ,cRelatedTag)
WAIT WINDOW PROGRAM() + CHR(13) + ;
    "Child: " + cTableName + "." + cRelatedChild + CHR(13) + ;
    "Parent: " + cRelatedTable + "." + cRelatedTag

PROCEDURE DBC_BeforeAddRelation(cRelationID, cTableName, ;
    cRelatedChild, cRelatedTable ,cRelatedTag)
WAIT WINDOW PROGRAM() + CHR(13) + ;
    "Child: " + cTableName + "." + cRelatedChild + CHR(13) + ;
    "Parent: " + cRelatedTable + "." + cRelatedTag

* End of stored procedures.
* Open the database and turn on DBC events.

OPEN DATABASE TestData
DBSetProp('TestData', 'Database', 'DBCEvents', .T.)

* Change an existing relation to see events fire.

ALTER TABLE Orders ALTER COLUMN Cust_ID C(6) REFERENCES Customer
```
### See Also

[Alter Table](s4g332.md), [Database Events](s4g900.md)